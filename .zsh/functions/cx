#autoload

# can't be bothered to support this on old zshs

if [[ "$ZSH_VERSION_TYPE" == 'old' ]]; then
  echo "\nThis zsh doesn't support typeset -T; disabling cx."
  cx () { }
  return 1
fi

cx () {
  local long_host short_host title_host short_from_opts

  long_host=${HOST}
  short_host=${HOST%%.*}

  if [[ "$1" == "-s" ]]; then
    short_from_opts=yes
    shift
  fi

  if [[ ${+CX_SHORT_HOSTNAMES} -eq 1 || "$short_from_opts" == "yes" ]]; then
    title_host=$short_host
  else
    title_host=$long_host
  fi

  if [[ "$ZSH_VERSION_TYPE" == 'new' ]]; then
    (( $+title )) || typeset -gT TITLE title
    (( $+title )) || typeset -gT ITITLE ititle
  fi

  : ${TITLE_SHLVL:=$SHLVL}
  export TITLE_SHLVL

  if [[ "$SHLVL" != "$TITLE_SHLVL" ]]; then
    # We've changed shell; assume that the most recently pushed entry
    # is the starting piont for the new shell.
    TITLE_SHLVL=$SHLVL
    [[  ${(t)title} == 'array' ]] &&  title=( "$title[1]" )
    [[ ${(t)ititle} == 'array' ]] && ititle=( "$ititle[1]" )
  fi

  suffix="$USERNAME@${title_host}"
  isuffix="$USERNAME@${short_host}"

  export TITLE ITITLE

  if (( $# == 0 )); then
    # restore current setting
    if (( $#title == 0 )); then
      full_title="$suffix"
      full_ititle="$isuffix"
    else
      full_title="$title[1] : $suffix"
      full_ititle="$ititle[1] : $isuffix"
    fi
  else
    # push new setting
    if (( $#title )); then
      title=(  "$*" "$title[@]" )
      ititle=( "$*" "$ititle[@]" )
    else
      title=( "$*" )
      ititle=( "$*" )
    fi

    if [[ -z "$*" ]]; then
      # allow pushing of ""
      full_title="$suffix"
      full_ititle="$isuffix"
    else
      full_title="$* : $suffix"
      full_ititle="$* : $isuffix"
    fi
  fi

  set_title window "$full_title"
  set_title icon "$full_ititle"
  [[ -n "$IN_SCREEN" ]] && 
    set_title screen "$full_ititle" # "${title:-$isuffix}"
  return 0
}

cx "$@"
